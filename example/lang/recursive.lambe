--{
    Recursive schema definition and fold implementation
}

data Fix (f:type->type) { unfix: f (Fix f) }

impl forall (f:type->type). Fix f with Functor f {
    sig fold : forall a. self -> (f a -> a) -> a
    def fold interpret = interpret $ { _ fold interpret } map self.unfix
}

-- Simple logical expression

data Boolean { value : Bool }
data Or  a { left : a; right : a }
data And a { left : a; right : a }

type Exp a = Boolean | Or a | And a

-- Associated functor | Can generate a free monad right now

impl Functor Exp {
    def map e =
        when e
        is Boolean -> e
        is Or -> Or (self e.left) (self e.right)
        is And -> And (self e.left) (self e.right)
}

-- Evaluations

impl Exp string {
    sig pretty : self -> string
    def pretty =
        when self
            is Boolean -> self.value to_string
            is Or -> "(%1 || %2)" self.left self.right
            is And -> "(%1 && %2)" self.left self.right
}

impl Exp bool {
    sig eval : self -> bool
    def eval =
        when self
            is Boolean -> self.value
            is Or -> self.left || self.right
            is And -> self.left && self.right
}

-- Smart constructors

impl Fix Exp {
    sig bool : bool -> self
    def bool b = Fix (Boolean b)

    sig (||) : self -> self -> self
    def (||) r = Fix (Or self r)

    sig (&&) : self -> self -> self
    def (&&) r = Fix (And self r)
}

--{
    The code above is therefore simply used. For instance, the
    expression `(bool true) && (bool false) fold { _ pretty }`
    returns a string representation and the expression
    `(bool true) && (bool false) fold { _ eval }` returns a boolean
}
